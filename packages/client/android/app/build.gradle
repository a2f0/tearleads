apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'

android {
    namespace "com.tearleads.app"
    compileSdk rootProject.ext.compileSdkVersion
    buildFeatures {
        buildConfig true
    }
    defaultConfig {
        applicationId "com.tearleads.app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 990
        versionName "1.0.990"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        aaptOptions {
             // Files and dirs to omit from the packaged assets dir, modified to accommodate modern web apps.
             // Default: https://android.googlesource.com/platform/frameworks/base/+/282e181b58cf72b6ca770dc7ca5f91f135444502/tools/aapt/AaptAssets.cpp#61
            ignoreAssetsPattern '!.svn:!.git:!.ds_store:!*.scc:.*:!CVS:!thumbs.db:!picasa.ini:!*~'
        }
    }
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def hasReleaseKeystore = keystorePropertiesFile.exists()
    signingConfigs {
        release {
            if (hasReleaseKeystore) {
                def keystoreProperties = new Properties()
                keystorePropertiesFile.withInputStream { keystoreProperties.load(it) }
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }
    buildTypes {
        release {
            signingConfig hasReleaseKeystore ? signingConfigs.release : signingConfigs.debug
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            testProguardFiles 'proguard-rules-test.pro'
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
        // Build type for running instrumented tests against minified code.
        // Includes proguard-rules-test.pro in the main APK to preserve classes
        // needed by the test framework. This keeps production APK lean. See #750.
        releaseInstrumented {
            initWith release
            signingConfig hasReleaseKeystore ? signingConfigs.release : signingConfigs.debug
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-rules-test.pro'
            matchingFallbacks = ['release']
            ndk {
                debugSymbolLevel 'FULL'
            }
        }
    }
    def testBuildTypeOverride = project.findProperty('android.testBuildType')
    if (testBuildTypeOverride) {
        testBuildType testBuildTypeOverride
    }
}

repositories {
    flatDir{
        dirs '../capacitor-cordova-android-plugins/src/main/libs', 'libs'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "androidx.coordinatorlayout:coordinatorlayout:$androidxCoordinatorLayoutVersion"
    implementation "androidx.media:media:$androidxMediaVersion"
    implementation "androidx.core:core-splashscreen:$coreSplashScreenVersion"
    implementation project(':capacitor-android')
    // Required for @capawesome/capacitor-file-picker
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.1'
    implementation "androidx.tracing:tracing:1.2.0"
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
    implementation project(':capacitor-cordova-android-plugins')
}

apply from: 'capacitor.build.gradle'

try {
    def servicesJSON = file('google-services.json')
    if (servicesJSON.text) {
        apply plugin: 'com.google.gms.google-services'
    }
} catch(Exception e) {
    logger.info("google-services.json not found, google-services plugin not applied. Push Notifications won't work")
}
